/// How to read this:
/// Each rule starts with a name, then a colon, and contains rules. Each rule ends with a semicolon.
/// More than one possible rule in one place can be indicated by separating them with "|".
/// Literals are enclosed in "".
/// Repeating rules can be indicated with * after them and a separator using [] and a literal inside it.
/// Optional rules can be indicated with ? after them.
/// First brackets can be used to pack multiple rules into a single rule without defining a new rule with a name.
/// <> implies that no space is allowed between two rules.
/// ! before * implies that rule must repeat at least once.
/// <~ ~> describes something that can only be described with words :^).

program: (decl | impl)*;
decl: "pub"? (struct_decl | enum_decl | const_stmt | let_stmt | fn_decl | mod_decl | trait_decl);
statement: (expression ";") | enum_decl | struct_decl | impl | mod_decl | use | for | loop | while | conditional | fn_decl |
            let_stmt | const_stmt | trait_decl | type_alias;

enum_decl: "enum" ident generic_params_without_trait_bound? "{"
                (
                    (ident("(" ty_path_non_expr ")")?)
                    | ident "{" ident ":" ty_path_non_expr "}")
                    | ident
                )*[","]!
            "}" ;
struct_decl: "struct" ident generic_params_without_trait_bound? "{" (ident ":" ty_path_non_expr)*[","] "}";
impl: "impl" generic_params_with_trait_bound? (ty_path_non_expr "for")? ty_path "{" (const_stmt | fn_decl | type_alias)!* "}";

fn_decl: fn_prototype body;
fn_prototype: "fn" ident generic_params_with_trait_bound? "(" (("self" ("," fn_arg)) | fn_arg*[","])?      ")" ("->" ty_path_non_expr)? ")"
                "where" (ident ":" ty_path_non_expr)*[","];
fn_arg: (("anon" | "vararg") ident ":" ("mut" | "*")? ty_path_non_expr);
ty_path_non_expr: ident*["::"] ("::"? generic_params_without_trait_bound);

trait_decl: "trait" ident ("<" ty_path_non_expr)? generic_params_with_trait_bound? "{" (type_alias | fn_decl)* "}";
let_stmt: "let" "mut"? ident (":" ty_path_non_expr)? ("=" expression)?;
const_stmt: "const" ident (":" ty_path_non_expr) ("=" expression)?;
type_alias: "type" ident generic_params_with_trait_bound ty_path_non_expr;
use: "use" use_tree;
use_tree: ident "::" (use_tree | ("{" use_tree*[","] "}"))?;
mod_decl: "mod" ident ("{" program "}" | ";");
for: "for" ident "in" expression body;
while: "while" expression body;
loop: "loop" body;
/// FIXME: using a body shouldn't actually require a comma separator,
///        but currently grammar says so due to the limitation of the
///        imaginary grammar format I've come up with :^)
match: "match" expression "{" (match_cond_and_fat_arrow (expression | body)*[,]! "}"
match_cond_and_fat_arrow: match_cond "=>" ;
match_cond: "_"
            | true
            | false
            | (bin_number | oct_number | hex_number | dec_number)*["|"]!
            | str*["|"]!
            | ty_path "(" ident ")"
            | ident "if" expression ;
conditional: "if" expression body ("else" (conditional | body)?)?;

body: "{" statement* expression? "}";

generic_params_without_trait_bound: "<" ident*[,] ">";
generic_params_with_trait_bound: "<" (ident (":" ty_path)?)*[","] ">";

expression: logical_or (("=" | "+=" | "-=" | "*=" | "/=" | ">>=" | "<<=" | "&=" | "|=" | "^=" | "**=" ) expression)?;
logical_or: logical_and ("||" logical_and)*;
logical_and: equality ("&&" equality)*;
equality: comparison (("==" | "!=") comparison)*;
comparison: bitwise_or ((">" | ">=" | "<" | "<=") bitwise_or)*;
bitwise_or: bitwise_xor ("|" bitwise_xor)*;
bitwise_xor: bitwise_and ("^" bitwise_and)*;
bitwise_and: bitwise_shift ("&" bitwise_shift)*;
bitwise_shift: term ((">>" | "<<") term)*;
term: factor (("+" | "-") factor)*;
factor: power (("*" | "/") power)*;
power: unary ("**" unary)*;
unary: ("!" | "-" | "~")? (unary | index);
index: primary ("[" bin_number | hex_number | oct_number | dec_number "]")*;
fn_call: primary (("(" ((ident ":")? expression)*[","] ")") | "." ident)*;
primary: "(" expression ")" | ty_path | array_literal | hex_number | oct_number | bin_number | dec_number | true | false | str;
array_literal: "[" expression*[","] "]";
ty_path: ident*["::"] ("::" generic_params_without_trait_bound);
ident: UTF8_ALPHABET<>("_" | "$" | UTF8_ALPHANUM)<>*;
bin_number: "0b"<>("0" | "1")<>!*;
hex_number: "0x"<>(digit | "A" | "B" | "C" | "D" | "E" | "F" | "a" | "b" | "c" | "d" | "e" | "f")<>!*;
oct_number: "0o"<>("0" | "1" | "2" | "3" | "4" | "5" | "6" | "7")<>!*;
dec_number: digit<>!*<>("." digit<>*)?;
digit: "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" | "_";
true: "true";
false: "false";
str: "\"" <~ anything, literally anything ~> "\"";

UTF8_ALPHABET: <~ valid utf8 alphabets ~> ;
UTF8_NUM: <~ valid utf8 numbers ~> ;
UTF8_ALPHANUM: UTF8_ALPHABET | UTF8_NUM;
